plugins {
    id 'war'
}
description = 'Personnel Account Manager app'
group 'org.jlab'
version '3.0.0'
ext.releaseDate = 'Jan 8 2024'
tasks.withType(JavaCompile) {
    options.release = 11
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-Xlint:deprecation", "-Xlint:unchecked"]
}
repositories {
    mavenCentral()
}
dependencies {
    implementation 'org.wildfly.security:wildfly-elytron:1.19.1.Final'

    // No matter what smoothness weblib MUST be included in WAR (https://github.com/JeffersonLab/smoothness/issues/4)
    implementation 'org.jlab:smoothness-weblib:4.3.0'

    // Smoothness setup should install these into Wildfly
    providedCompile 'org.tuckey:urlrewritefilter:4.0.4'

    // Wildfly 26.1.3.Final provides:
    providedCompile 'javax:javaee-api:8.0.1',
                    'javax.servlet:jstl:1.2',
                    'org.hibernate:hibernate-core:5.3.28.Final'
}
war {
    archiveFileName = 'pam.war'
    filesMatching('WEB-INF/web.xml') {
        filter {
            String line -> line.replaceAll("@VERSION@", project.version)
        }
        filter {
            String line -> line.replaceAll("@RELEASE_DATE@", releaseDate)
        }
    }
}
